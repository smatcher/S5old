# SConscript_project

from scons_common import *

Import('env')

LIB_NAME = 'S5engine'

PROJECT_DIR = 'S5engine'
BUILD_DIR = 'bin/S5engine/' + getObjectFilesDir()

engineEnv = env.Clone()
engineEnv.Append(CPPPATH = [PROJECT_DIR+'/include'])

VariantDir(BUILD_DIR,PROJECT_DIR, duplicate=0)

# Read all sources
all_src_list	= map(lambda x: BUILD_DIR + '/' + x, getSourcesListFromFile(PROJECT_DIR+'/S5engine.files'))
# Read all headers
all_h_list	= getHeadersListFromFile(PROJECT_DIR+'/S5engine.files')

# Moc the files
engineEnv['QT4_AUTOSCAN'] = 0 # We scan manually
moc_list = []
for h in all_h_list:
	if isMocable(PROJECT_DIR + '/' + h):
		moc_list = moc_list + engineEnv.XMoc4(BUILD_DIR + '/' + h)

# Create the object files
obj_list   = engineEnv.Object(all_src_list + moc_list)

lib = engineEnv.StaticLibrary(BUILD_DIR + '/' + LIB_NAME,obj_list)

